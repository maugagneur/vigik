import kotlinx.kover.gradle.plugin.dsl.AggregationType
import kotlinx.kover.gradle.plugin.dsl.CoverageUnit
import kotlinx.kover.gradle.plugin.dsl.GroupingEntityType

plugins {
    id("org.jetbrains.kotlinx.kover.aggregation") version "0.9.1"
}

dependencyResolutionManagement {
    repositories {
        google()
        mavenCentral()
    }
    versionCatalogs {
        create("libs") {
            from(files("../gradle/libs.versions.toml"))
        }
    }
}

kover {
    enableCoverage()

    // Report generation task can be run with "./gradlew koverHtmlReportDebug"
    reports {
        excludesAnnotatedBy.addAll(
            "androidx.compose.runtime.Composable",
            "com.kidor.vigik.utils.ExcludedFromKoverReport"
        )
        excludedClasses.addAll(
            "*Activity*",           // Ignore activities
            "*.*Application*",      // Ignore application class
            "*.BuildConfig",
            "*.*_Factory*",         // Ignore ViewModel factory classes generated by Hilt
            "*.*_MembersInjector*", // Ignore Hilt generated classes for receivers
            "*.*Hilt*",             // Ignore other Hilt generated classes
            "*.*_Impl*"             // Ignore Room and Worker generated classes
        )
        excludesInheritedFrom.addAll(
            "hilt_aggregated_deps",         // Ignore Hilt generated files
            "com.kidor.vigik.car",          // Ignore classes for Android Car
            "com.kidor.vigik.di",           // Ignore dependency injection module classes
            "com.kidor.vigik.ui.compose"    // Ignore classes related to UI theme and navigation
        )

        // Verify task can be run with "./gradlew koverVerifyDebug"
        verify {
            rule {
                name = "Minimal line coverage rate"
                disabled = false
                groupBy = GroupingEntityType.APPLICATION
                bound {
                    aggregationForGroup = AggregationType.COVERED_PERCENTAGE
                    coverageUnits = CoverageUnit.LINE
                    minValue = 60
                }
            }
        }
    }
}

rootProject.name = "build-logic"
include(":convention")
