plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp' version "$kspVer"
    id 'jacoco'
    id 'io.gitlab.arturbosch.detekt'
    id 'org.cqfn.diktat.diktat-gradle-plugin'
}

android {
    compileSdk sdkVer.compile

    defaultConfig {
        applicationId "com.kidor.vigik"
        minSdk sdkVer.min
        targetSdk sdkVer.target
        versionCode 1
        versionName "1.0.0-alpha.1"

        testInstrumentationRunner "com.kidor.vigik.utils.HiltTestRunner"

        ksp {
            arg('room.schemaLocation', "$projectDir/schemas")
            arg('room.incremental', "true")
            arg('room.expandProjection', "true")
        }
    }

    buildFeatures {
        compose true
        viewBinding true
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    configurations.all{
        resolutionStrategy {
            eachDependency { details ->
                if ('org.jacoco' == details.requested.group) {
                    details.useVersion jacocoVer
                }
            }
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion androidxVer.composeCompiler
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true",
                "-opt-in=kotlin.RequiresOptIn"
        ]
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

jacoco {
    toolVersion = jacocoVer
    getReportsDirectory().set(file("$buildDir/reports/customJacocoReport"))
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

detekt {
    toolVersion = detektVer
    buildUponDefaultConfig = true // preconfigure defaults
    allRules = false // activate all available (even unstable) rules.
    config = files("$projectDir/config/detekt/config.yml") // point to your custom config defining rules to run, overwriting default behavior
    //baseline = file("$projectDir/config/baseline.xml") // a way of suppressing issues before introducing detekt
    ignoreFailures = true // If set to `true` the build does not fail when the maxIssues count was reached.
}

diktat {
    reporter = "html" // "html", "json", "plain" (default), "sarif"
    output = "$project.projectDir/build/reports/diktat/diktat.html"
    ignoreFailures = true
}

// Custom task to merge code coverage reports from unit and instrumentation tests
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    reports {
        html.required = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            '**/*Args*.*',          // filtering Navigation Component generated classes
            '**/*Directions*.*',    // filtering Navigation Component generated classes
            '**/databinding/**',    // filtering Data Binding generated classes
            'android/**/*.*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))
    getClassDirectories().setFrom(files([debugTree]))
    getExecutionData().setFrom(
            fileTree(dir: project.buildDir, includes: [
                    'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
                    'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
            ])
    )
}

dependencies {
    // Annotation processor
    implementation "com.google.devtools.ksp:symbol-processing-api:${kspVer}"

    // Android X
    implementation "androidx.activity:activity-ktx:${androidxVer.activity}"
    implementation "androidx.appcompat:appcompat:${androidxVer.appCompat}"
    implementation "androidx.constraintlayout:constraintlayout:${androidxVer.constraintlayout}"
    implementation "androidx.core:core-ktx:${androidxVer.coreKtx}"
    implementation "androidx.core:core-splashscreen:${androidxVer.splashscreen}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${androidxVer.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${androidxVer.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${androidxVer.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${androidxVer.lifecycle}"
    implementation "androidx.navigation:navigation-compose:${androidxVer.navigation}"
    implementation "androidx.navigation:navigation-fragment-ktx:${androidxVer.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${androidxVer.navigation}"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:${androidxVer.navigation}"
    implementation "androidx.recyclerview:recyclerview:${androidxVer.recyclerview}"

    // Compose
    implementation "androidx.compose.foundation:foundation:${androidxVer.compose}"
    implementation "androidx.compose.material:material:${androidxVer.compose}"
    implementation "androidx.compose.material:material-icons-extended:${androidxVer.compose}"
    implementation "androidx.compose.material3:material3:${androidxVer.material3}"
    implementation "androidx.compose.runtime:runtime:${androidxVer.compose}"
    implementation "androidx.compose.runtime:runtime-livedata:${androidxVer.compose}"
    implementation "androidx.compose.ui:ui:${androidxVer.compose}"
    implementation "androidx.compose.ui:ui-tooling:${androidxVer.compose}"
    implementation "androidx.compose.ui:ui-tooling-preview:${androidxVer.compose}"

    // Database
    implementation "androidx.room:room-runtime:${androidxVer.room}"
    implementation "androidx.room:room-ktx:${androidxVer.room}"
    ksp "androidx.room:room-compiler:${androidxVer.room}"

    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlinVer.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${kotlinVer.coroutines}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:${kotlinVer.coroutines}"

    // Material
    implementation "com.google.android.material:material:${androidxVer.material}"

    // Dependency Injection
    implementation "com.google.dagger:hilt-android:${androidxVer.hilt}"
    kapt "com.google.dagger:hilt-compiler:${androidxVer.hilt}"
    androidTestImplementation "com.google.dagger:hilt-android-testing:${androidxVer.hilt}"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:${androidxVer.hilt}"

    // Log
    implementation "com.jakewharton.timber:timber:$timberVer"

    // Test
    testImplementation "junit:junit:${testVer.junit}"
    testImplementation "org.mockito.kotlin:mockito-kotlin:${testVer.mockitoKotlin}"
    testImplementation "androidx.arch.core:core-testing:${testVer.coreTesting}"
    testImplementation "io.mockk:mockk:${testVer.mockK}"
    testImplementation "app.cash.turbine:turbine:${testVer.turbine}"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:${androidxVer.compose}"
    androidTestImplementation "androidx.test.ext:junit:${testVer.testExt}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${testVer.espresso}"
    androidTestImplementation "org.mockito:mockito-android:${testVer.mockitoAndroid}"

    // Testing Navigation
    // debugImplementation is used here so that the empty activity that FragmentScenario relies on is accessible by the test target process.
    debugImplementation "androidx.compose.ui:ui-test-manifest:${androidxVer.compose}"
    debugImplementation "androidx.fragment:fragment-testing:${androidxVer.fragment}"
    androidTestImplementation "androidx.navigation:navigation-testing:${androidxVer.navigation}"
}